 .sect "L3_PROGRAM"
 
 .global _ContrastBrightSaturationStatTable_sa

_ContrastBrightSaturationStatTable_sa: .cproc  pOutBuf, pInBuf, DstStride, SrcStride, Width, Height, LUTable, SATTable, HistTable
  .reg  IN_32:IN_10, OUT_32:OUT_10, loop_i, loop_j, IN_Stride, OUT_Stride
  .reg  Val_Y0, Val_Y1, Val_Y2, Val_Y3, Val_Cb0, Val_Cr0, Val_Cb1, Val_Cr1
  .reg  Idx_Y0, Idx_Y1, Idx_Y2, Idx_Y3, Idx_Cb0, Idx_Cr0, Idx_Cb1, Idx_Cr1
  .reg  LUT_Y0, LUT_Y1, LUT_Y2, LUT_Y3, LUT_Y1Y0, LUT_Y3Y2
  .reg  LUT_Cb0, LUT_Cr0, LUT_Cb1, LUT_Cr1, LUT_Cr0Cb0, LUT_Cr1Cb1
  .reg  OUT_Y1Y0, OUT_Y3Y2 
  .reg  Width2, Mask, Mask_c, Mask2, Mask2_c
  .reg  LUTable1, LUTable2, LUTable3, SATTable1, SATTable2, SATTable3
  .reg  HistTable1, HistTable2, HistTable3
  .reg  Stat_Y0, Stat_Y1, Stat_Y2, Stat_Y3
  .reg  Stat_AddY0, Stat_AddY1, Stat_AddY2, Stat_AddY3
  .reg  Cmp_Y0Y1, Cmp_Y2Y3
  .reg  HistOffset, TMP_32:TMP_10

    
  SHL     Width,     0x1,      Width2
  SUB     SrcStride, Width2,   IN_Stride  
  SUB     DstStride, Width2,   OUT_Stride  
  SUB     Height,    0x1,      loop_j
  SHR     Width,     0x2,      Width
  MVK     0xff,      Mask  
  MVK     0xff,      Mask_c
  PACK2   Mask,      Mask,     Mask2
  PACK2   Mask_c,    Mask_c,   Mask2_c       
  MV      LUTable,   LUTable1
  MV      LUTable,   LUTable2
  MV      LUTable,   LUTable3
  MV      SATTable,  SATTable1
  MV      SATTable,  SATTable2
  MV      SATTable,  SATTable3
  MVKL    0x400,     HistOffset
  MVKLH   0x0,       HistOffset
  MV      HistTable, HistTable1 
  ADD     HistTable, HistOffset, HistTable2 
  ADD     HistTable, HistOffset, HistTable3

  .mdep st1, ld1 
  .mdep st2, ld2 
  .mdep st3, ld3  
  .mdep st4, ld4 

ContrastBrightSaturationStatTLoop1:
  SUB     Width,  0x1,      loop_i   

ContrastBrightSaturationStatTLoop0: 
  LDNDW    *pInBuf++, IN_32:IN_10
  SHR     IN_10,     0x8,   Val_Y0 
  SHR     IN_10,     0x10,  Val_Cr0 
  SHR     IN_10,     0x18,  Val_Y1
  SHR     IN_32,     0x8,   Val_Y2 
  SHR     IN_32,     0x10,  Val_Cr1 
  SHR     IN_32,     0x18,  Val_Y3 

  AND     Val_Y0,    Mask,    Idx_Y0
  AND     Val_Y1,    Mask,    Idx_Y1
  AND     IN_10,     Mask,    Idx_Cb0
  AND     Val_Cr0,   Mask,    Idx_Cr0
  AND     Val_Y2,    Mask_c,  Idx_Y2
  AND     Val_Y3,    Mask_c,  Idx_Y3
  AND     IN_32,     Mask_c,  Idx_Cb1
  AND     Val_Cr1,   Mask_c,  Idx_Cr1
  CMPEQ   Idx_Y0,    Idx_Y1,  Cmp_Y0Y1  
  CMPEQ   Idx_Y2,    Idx_Y3,  Cmp_Y2Y3  

  LDBU    *+LUTable[Idx_Y0],   LUT_Y0
  LDBU    *+LUTable1[Idx_Y1],  LUT_Y1
  LDBU    *+LUTable2[Idx_Y2],  LUT_Y2
  LDBU    *+LUTable3[Idx_Y3],  LUT_Y3
  LDBU    *+SATTable[Idx_Cb0],   LUT_Cb0
  LDBU    *+SATTable1[Idx_Cr0],  LUT_Cr0
  LDBU    *+SATTable2[Idx_Cb1],  LUT_Cb1
  LDBU    *+SATTable3[Idx_Cr1],  LUT_Cr1
  LDW     *+HistTable[Idx_Y0] {ld1},  Stat_Y0
  LDW     *+HistTable1[Idx_Y1] {ld2}, Stat_Y1
  LDW     *+HistTable2[Idx_Y2] {ld3},  Stat_Y2
  LDW     *+HistTable3[Idx_Y3] {ld4}, Stat_Y3

  [!Cmp_Y0Y1]ADD     Stat_Y0,   0x1,      Stat_AddY0
  [!Cmp_Y0Y1]ADD     Stat_Y1,   0x1,      Stat_AddY1
  [Cmp_Y0Y1]ADD      Stat_Y0,   0x2,      Stat_AddY0
  [Cmp_Y0Y1]ADD      Stat_Y1,   0x2,      Stat_AddY1
  [!Cmp_Y2Y3]ADD     Stat_Y2,   0x1,      Stat_AddY2
  [!Cmp_Y2Y3]ADD     Stat_Y3,   0x1,      Stat_AddY3
  [Cmp_Y2Y3]ADD      Stat_Y2,   0x2,      Stat_AddY2
  [Cmp_Y2Y3]ADD      Stat_Y3,   0x2,      Stat_AddY3
  
  PACKL4  LUT_Y1,    LUT_Y0,   LUT_Y1Y0  
  PACKL4  LUT_Y3,    LUT_Y2,   LUT_Y3Y2  
  PACKL4  LUT_Cr0,   LUT_Cb0,  LUT_Cr0Cb0  
  PACKL4  LUT_Cr1,   LUT_Cb1,  LUT_Cr1Cb1  

  SHL     LUT_Y1Y0,  0x8,      OUT_Y1Y0            
  SHL     LUT_Y3Y2,  0x8,      OUT_Y3Y2   

  OR      OUT_Y1Y0,  LUT_Cr0Cb0,   OUT_10 
  OR      OUT_Y3Y2,  LUT_Cr1Cb1,   OUT_32 

  STNDW    OUT_32:OUT_10,   *pOutBuf++
  STW     Stat_AddY0,  *+HistTable[Idx_Y0] {st1}
  STW     Stat_AddY1,  *+HistTable1[Idx_Y1] {st2}
  STW     Stat_AddY2,  *+HistTable2[Idx_Y2] {st3}
  STW     Stat_AddY3,  *+HistTable3[Idx_Y3] {st4} 

  [loop_i]BDEC    ContrastBrightSaturationStatTLoop0,  loop_i  

  ADD     pInBuf, IN_Stride,     pInBuf   
  ADD     pOutBuf, OUT_Stride,   pOutBuf
  [loop_j]BDEC    ContrastBrightSaturationStatTLoop1,  loop_j

 .endproc