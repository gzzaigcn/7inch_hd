 .sect "L3_PROGRAM"
 
 .global _ColorTransStatTable_sa

_ColorTransStatTable_sa: .cproc  pOutBuf, pInBuf, DstStride, SrcStride, Width, Height, LUTableY, LUTableCb, LUTableCr, HistTable
  .reg  IN_32:IN_10, OUT_32:OUT_10, loop_i, loop_j, IN_Stride, OUT_Stride
  .reg  Val_Y0, Val_Y1, Val_Y2, Val_Y3
  .reg  Idx_Y0, Idx_Y1, Idx_Y2, Idx_Y3
  .reg  LUT_Y0, LUT_Y1, LUT_Y2, LUT_Y3, LUT_Y0Cb0, LUT_Y1Cr0, LUT_Y2Cb1, LUT_Y3Cr1
  .reg  LUT_Cb0, LUT_Cr0, LUT_Cb1, LUT_Cr1
  .reg  LUT_Cb0c, LUT_Cr0c, LUT_Cb1c, LUT_Cr1c
  .reg  OUT_Y1Y0, OUT_Y3Y2 
  .reg  Val_CBCR, Val_CBCR_c, Width2, Mask, Mask_c
  .reg  LUTableY1, LUTableY2, LUTableY3
  .reg  LUTableCb1, LUTableCr1
  .reg  HistTable1, HistTable2, HistTable3
  .reg  Stat_Y0, Stat_Y1, Stat_Y2, Stat_Y3
  .reg  Stat_AddY0, Stat_AddY1, Stat_AddY2, Stat_AddY3
  .reg  Cmp_Y0Y1, Cmp_Y2Y3
  .reg  HistOffset, TMP_32:TMP_10
  .reg  Cmp0, Cmp1, Cmp2, Cmp3 

  SHL     Width,     0x1,      Width2
  SUB     SrcStride, Width2,   IN_Stride  
  SUB     DstStride, Width2,   OUT_Stride  
  SUB     Height,    0x1,      loop_j
  SHR     Width,     0x2,      Width
  MVK     0xff,      Mask  
  MVK     0xff,      Mask_c
  MVKL    0x400,     HistOffset
  MVKLH   0x0,       HistOffset
  MV      LUTableY,  LUTableY1
  MV      LUTableY,  LUTableY2
  MV      LUTableY,  LUTableY3
  MV      LUTableCb, LUTableCb1
  MV      LUTableCr, LUTableCr1
  MV      HistTable, HistTable1 
  ADD     HistTable, HistOffset, HistTable2 
  ADD     HistTable, HistOffset, HistTable3

  .mdep st1, ld1 
  .mdep st2, ld2 
  .mdep st3, ld3 
  .mdep st4, ld4 
  
ColorTTSLoop1:
  SUB     Width,  0x1,      loop_i   

ColorTTSLoop0: .trip   4
  LDNDW    *pInBuf++, IN_32:IN_10
  SHR     IN_10,     0x8,   Val_Y0 
  SHR     IN_10,     0x18,  Val_Y1
  SHR     IN_32,     0x8,   Val_Y2 
  SHR     IN_32,     0x18,  Val_Y3 
  
  AND     Val_Y0,    Mask,    Idx_Y0
  AND     Val_Y1,    Mask,    Idx_Y1
  AND     Val_Y2,    Mask_c,  Idx_Y2
  AND     Val_Y3,    Mask_c,  Idx_Y3
  CMPEQ   Idx_Y0,    Idx_Y1,  Cmp_Y0Y1  
  CMPEQ   Idx_Y2,    Idx_Y3,  Cmp_Y2Y3  
   
  LDBU    *+LUTableY[Idx_Y0],   LUT_Y0
  LDBU    *+LUTableY1[Idx_Y1],  LUT_Y1
  LDBU    *+LUTableY2[Idx_Y2],  LUT_Y2
  LDBU    *+LUTableY3[Idx_Y3],  LUT_Y3
  LDW     *+HistTable[Idx_Y0] {ld1},  Stat_Y0
  LDW     *+HistTable1[Idx_Y1] {ld2}, Stat_Y1
  LDW     *+HistTable[Idx_Y2] {ld3},  Stat_Y2
  LDW     *+HistTable1[Idx_Y3] {ld4}, Stat_Y3
  
  [!Cmp_Y0Y1]ADD     Stat_Y0,   0x1,      Stat_AddY0
  [!Cmp_Y0Y1]ADD     Stat_Y1,   0x1,      Stat_AddY1
  [Cmp_Y0Y1]ADD      Stat_Y0,   0x2,      Stat_AddY0
  [Cmp_Y0Y1]ADD      Stat_Y1,   0x2,      Stat_AddY1
  [!Cmp_Y2Y3]ADD     Stat_Y2,   0x1,      Stat_AddY2
  [!Cmp_Y2Y3]ADD     Stat_Y3,   0x1,      Stat_AddY3
  [Cmp_Y2Y3]ADD      Stat_Y2,   0x2,      Stat_AddY2
  [Cmp_Y2Y3]ADD      Stat_Y3,   0x2,      Stat_AddY3

  LDBU    *+LUTableCb[Idx_Y0],   LUT_Cb0
  LDBU    *+LUTableCr[Idx_Y0],   LUT_Cr0
  LDBU    *+LUTableCb[Idx_Y1],   LUT_Cb0c
  LDBU    *+LUTableCr[Idx_Y1],   LUT_Cr0c
  LDBU    *+LUTableCb1[Idx_Y2],  LUT_Cb1
  LDBU    *+LUTableCr1[Idx_Y2],  LUT_Cr1
  LDBU    *+LUTableCb1[Idx_Y3],  LUT_Cb1c
  LDBU    *+LUTableCr1[Idx_Y3],  LUT_Cr1c

  CMPEQ   LUT_Y0,    Mask,       Cmp0  
  CMPEQ   LUT_Y0,    0x0,        Cmp1 
  CMPEQ   LUT_Y2,    Mask,       Cmp2  
  CMPEQ   LUT_Y2,    0x0,        Cmp3  

  PACKL4  LUT_Y0,    LUT_Cb0,    LUT_Y0Cb0
  PACKL4  LUT_Y1,    LUT_Cr0,    LUT_Y1Cr0
  PACKL4  LUT_Y2,    LUT_Cb1,    LUT_Y2Cb1
  PACKL4  LUT_Y3,    LUT_Cr1,    LUT_Y3Cr1
  [Cmp0]PACKL4  LUT_Y0,    LUT_Cb0c,    LUT_Y0Cb0
  [Cmp0]PACKL4  LUT_Y1,    LUT_Cr0c,    LUT_Y1Cr0
  [Cmp1]PACKL4  LUT_Y0,    LUT_Cb0c,    LUT_Y0Cb0
  [Cmp1]PACKL4  LUT_Y1,    LUT_Cr0c,    LUT_Y1Cr0
  [Cmp2]PACKL4  LUT_Y2,    LUT_Cb1c,    LUT_Y2Cb1
  [Cmp2]PACKL4  LUT_Y3,    LUT_Cr1c,    LUT_Y3Cr1
  [Cmp3]PACKL4  LUT_Y2,    LUT_Cb1c,    LUT_Y2Cb1
  [Cmp3]PACKL4  LUT_Y3,    LUT_Cr1c,    LUT_Y3Cr1

  PACKL4  LUT_Y1Cr0, LUT_Y0Cb0,  OUT_10
  PACKL4  LUT_Y3Cr1, LUT_Y2Cb1,  OUT_32

  STNDW    OUT_32:OUT_10,   *pOutBuf++
  STW     Stat_AddY0,  *+HistTable[Idx_Y0] {st1}
  STW     Stat_AddY1,  *+HistTable1[Idx_Y1] {st2}
  STW     Stat_AddY2,  *+HistTable2[Idx_Y2] {st3}
  STW     Stat_AddY3,  *+HistTable3[Idx_Y3] {st4}

  [loop_i]BDEC    ColorTTSLoop0,  loop_i  

  ADD     pInBuf, IN_Stride,     pInBuf   
  ADD     pOutBuf, OUT_Stride,   pOutBuf
  [loop_j]BDEC    ColorTTSLoop1,  loop_j

 .endproc